import java.nio.file.FileSystems
import java.nio.file.Files
import java.nio.file.Paths

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.8'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.arnor4eck'
version = '0.0.1-SNAPSHOT'
description = 'ShortLinks application'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// DataBase
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'

	// Security
	implementation 'org.springframework.security:spring-security-core'
	implementation 'org.springframework.boot:spring-boot-starter-security'

	// Cache
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'com.github.ben-manes.caffeine:caffeine'

	// Tests
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}


def readProperties() {
	def envFile = file("${projectDir}/.env")
	def envProps = [:]

	if(envFile.exists()){
		def envLines = envFile.readLines()

		for (i in 0..< envLines.size()) {
			def line = envLines[i].trim()

			if(!line.isEmpty() && !line.startsWith("#")){
				def lindexOfComm = line.indexOf('#');
				if(lindexOfComm != -1)
					line = line.substring(0, lindexOfComm)

				String[] lineParts = line.split('=')

				if(lineParts.length == 2){
					def key = lineParts[0].trim()
					def value = lineParts[1].trim()

					envProps.put key, value
					System.setProperty(key, value)
					println "Системная переменная ${key} успешно установлена"
				}else
					println "Пропущена строка номер ${i + 1}"
			}
		}
	}else
		println "Файл .env не найден в корне проекта"

	return envProps
}

tasks.register('bootRunDev', org.springframework.boot.gradle.tasks.run.BootRun) {
	group = 'application'
	description = 'Run the Spring Boot application with dev profile'

	mainClass = bootRun.mainClass
	classpath = bootRun.classpath

	//dependsOn 'test'

	doFirst {
		def envProps = readProperties()
		envProps['spring.profiles.active'] = 'dev'
		envProps.each { key, value -> // системные свойства в JVM Gradle
			System.setProperty(key, value)
		}

		systemProperties = envProps  //systemProperties - это свойство задачи BootRun
	}
}

